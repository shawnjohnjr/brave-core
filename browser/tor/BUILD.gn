import("//brave/components/tor/buildflags/buildflags.gni")

source_set("tor") {
  # Remove when https://github.com/brave/brave-browser/issues/10655 is resolved
  check_includes = false
  sources = [
    "tor_profile_service_factory.cc",
    "tor_profile_service_factory.h",
  ]

  deps = [
    "//base",
    "//brave/components/tor",
    "//chrome/common",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
  ]

  public_deps = [
    "//brave/components/tor/buildflags",
  ]

  if (enable_tor) {
    sources += [
      "brave_tor_client_updater_delegate.cc",
      "brave_tor_client_updater_delegate.h",
      "onion_location_navigation_throttle.cc",
      "onion_location_navigation_throttle.h",
      "onion_location_tab_helper.cc",
      "onion_location_tab_helper.h",
      "tor_navigation_throttle.cc",
      "tor_navigation_throttle.h",
      "tor_tab_helper.cc",
      "tor_tab_helper.h",
    ]

    deps += [
      "//content/public/browser",
      "//content/public/common",
      "//extensions/common:common_constants",
    ]

    # Below dep list are not directly used tor target.
    # Only added to fix intermittent build failure.
    # Most of the case comes from including brave_browser_process_impl.h`
    # headers. That header includes some buildflags but sometimes they are
    # not generated before evaluating tor target.
    deps += [
      "//brave/components/brave_referrals/buildflags",
      "//brave/components/greaselion/browser/buildflags",
      "//brave/components/speedreader:buildflags",
    ]
  }
}
