import("//brave/components/tor/buildflags/buildflags.gni")

source_set("tor") {
  public_deps = [
    "//brave/components/tor/buildflags",
  ]

  sources = [
    "tor_constants.cc",
    "tor_constants.h",
    "tor_launcher_service_observer.h",
  ]

  deps = [
    ":pref_names",
    ":switches",
    "//base",
  ]

  if (enable_tor) {
    sources += [
      "brave_tor_client_updater.cc",
      "brave_tor_client_updater.h",
      "service_sandbox_type.h",
      "tor_control.cc",
      "tor_control.h",
      "tor_control_event.cc",
      "tor_control_event.h",
      "tor_control_event_list.h",
      "tor_launcher_factory.cc",
      "tor_launcher_factory.h",
      "tor_navigation_throttle.cc",
      "tor_navigation_throttle.h",
      "tor_profile_service.cc",
      "tor_profile_service.h",
      "tor_profile_service_impl.cc",
      "tor_profile_service_impl.h",
      "tor_tab_helper.cc",
      "tor_tab_helper.h",
      "onion_location_navigation_throttle.cc",
      "onion_location_navigation_throttle.h",
      "onion_location_tab_helper.cc",
      "onion_location_tab_helper.h",
    ]
  }
  
  deps += [
    "//brave/app:brave_generated_resources_grit",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/services/tor/public/interfaces",
    "//components/pref_registry",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//extensions/common:common_constants",
    "//net",
    "//services/service_manager",
    "//third_party/re2",
    "//url",
  ]
}

source_set("pref_names") {
  sources = [
    "pref_names.cc",
    "pref_names.h",
  ]
}

source_set("switches") {
  sources = [
    "tor_switches.cc",
    "tor_switches.h",
  ]
}

source_set("tor_unit_tests") {
  testonly = true
  if (enable_tor) {
    sources = [
      "tor_control_unittest.cc",
    ]

    deps = [
      "//base/test:test_support",
      "//brave/components/tor",
      "//brave/test:test_support",
      "//content/public/browser",
      "//content/test:test_support",
      "//testing/gtest",
    ]
  }
}
